---
layout: post
title: Much ado about Monads
published: true
---

This is a minimal tutorial on Monads. 

## Dictionary

The only syntax/concepts you need to understand 

| syntax | definition | parallel |
|-------|--------|---------|
| ```foo :: Int``` | ```Int``` type value ```foo``` | Types in any statically type language |
| ```foo :: Bar Int``` | Algebraic data type ```Bar``` composed with type ```Int``` | no clue (this is algebraic data types) | 
| ```foo :: Int -> String``` | Type signature of function ```foo``` from type ```Int``` to type ```String``` | Types in any statically type language |
| ```foo :: Int -> Int -> String``` | Type signature of two argument function ```foo``` from type ```Int, Int``` to type ```String``` | Types in any statically type language |
| ```foo :: a -> b``` | Type signature of generic ```foo``` from any type ```a``` to any type ```b``` | Types in any statically type language with generics |
| ```class Foo m where``` | Parametric type class ```Foo``` with type parameter ```m``` | ```public interface Foo``` in Java |
| ```class Baz m => Foo m where``` | Parametric type class ```Foo``` with type parameter ```m``` with bounded type m | ```public class Moo implements Foo, Baz``` in Java  |
| ```(+)``` | "prefixification" of an infix operator ```+``` | no clue |
| ```instance Foo Bar where``` | Instantiation of type class ```Foo``` by type ```Bar``` | ```public class Bar implements Foo``` |
| ```instance Foo (Bar e) where``` | Instantiation of type class ```Foo``` by parametric type ```Bar``` | ```public class Bar<E> implements Foo``` |
| ```instance Baz e => Foo (Bar e) where``` | Instantiation of type class ```Foo``` by parametric type ```Bar``` with bounded type e | ```public class Bar<e extends Bar> implements Foo``` |
| ```\x -> 2*x``` | Lambda function with parameter ```x``` | ```lambda x: 2*x``` in python |
| ```f x``` | ```f``` applied to ```x``` | ```f(x)```  |
| ```f $ g x``` | ```f``` applied to ```g``` applied to ```x``` (saves parentheses in ```f (g x)```)  | ```f(g(x))```  |
| ```f . g $ x``` | ```f``` composed with ```g``` | ```f(g(x))```  |
| ```data Env = Env { foo :: String, bar :: Int} deriving (Show)``` | Record | Structs in C++ with default ```cout <<``` representation (note that ```Env``` on the left is different from ```Env``` on the right - left is type, right is the constructor [and they could be named distinctly] ) |

You also need to understand algebraic data types but I'm not going to explain that.

## Carpenter's definition of Monads

A ```Monad``` is a type class whose instances implement the two functions/operators ```>>=``` and ```return``` (note: ```return``` is a function not a keyword)

```haskell
class Monad m where
  (>>=)       :: m a -> (a -> m b) -> m b 
  return      :: a -> m a
```

```>>=``` takes a value of the type ```m a``` (i.e. a value of the type that implements the interface) on the left and a function ```k :: a -> m b``` on the right and then in toto returns a new value ```m b``` of that same type. The function ```k :: a -> m b``` that takes a value of type ```a``` and returns a value of type ```m a```.

```return``` takes a value of type ```a``` and returns a type of ```m a```.

## Code

```haskell
import           Prelude                 hiding ( Monad(..) )
import           Data.Either
import qualified Prelude

class  Monad m where
  -- | Sequentially compose two actions, passing any value produced
  -- by the first as an argument to the second.
  (>>=)       :: m a -> (a -> m b) -> m b
  return      :: a -> m a

instance Monad Maybe where
    (Just x) >>= k = k x
    Nothing  >>= k = Nothing

    return = Just

desugared2 :: Maybe Int -> Maybe Int
desugared2 monkeyWrench =
  Just 3 >>= \a -> 
  monkeyWrench >>= \b -> 
  return $ a + b


instance Monad [] where
  m >>= f   =  concat (map f m)
  return x  =  [x]

m :: [Int]
m = [1, 2, 3, 4]

f :: Int -> [Int]
f = \x -> [1, 0]

demo = m >>= f

instance Monad (Either e) where
  return = Right
  Right m >>= k = k m
  Left e  >>= _ = Left e


demo1 = Left "boom" >>= \x -> return (x + 1)
demo2 = Right 100 >>= \x -> Left "no way!"

instance Monad ((->) r) where
  return a = (\_ -> a)
  m >>= k = \r -> (k (m r)) r

-- just returns the environment
ask :: a -> a
ask = id

-- transforms the env (usually used to access member in record)
asks :: (r -> a) -> (r -> a)
asks f = f

-- Executes a computation in a modified environment. 
local
  :: (r -> r) -- function to modify env
  -> (r -> a)  -- reader to run in the modified env
  -> (r -> a)
local f m = m . f


-- example

data MyContext = MyContext
  { foo :: String
  , bar :: Int
  } deriving (Show)

updateContext :: MyContext -> MyContext
updateContext m = m { foo = "foo" }

computation :: MyContext -> (Maybe String)
computation = asks bar >>= \n -> 
              local updateContext (asks foo) >>= \x -> 
              if n > 0 then 
                return (Just x) 
              else 
                return Nothing

ex1 :: Maybe String
ex1 = computation $ MyContext "hello" 1

ex2 :: Maybe String
ex2 = computation $ MyContext "haskell" 0






instance Monoid w => Monad ((,) w) where
    return a = (mempty, a)
    m >>= k =
        let (w, a)  = m
            (w', b) = k a
        in (w `mappend` w', b)

tell :: w -> (w, ())
tell w = (w, ())

type MyWriter = ([Int], String)

example :: MyWriter
example = do
  tell [1 .. 3]
  tell [3 .. 5]
  return "foo"

output :: ([Int], String)
output = example

newtype State s a = State (s -> (a,s))

instance Monad (State s) where
  return a = State $ \s -> (a, s)

  sm >>= k = State $ \s ->
    let (State st) = sm
        (a, s') = st s
        (State st') = k a
    in st' s'

get :: State s s
get = State $ \s -> (s, s)

put :: s -> State s ()
put s = State $ \_ -> ((), s)

modify :: (s -> s) -> State s ()
modify f = get >>= \x -> put (f x)

instance Monad IO where
  m >>= k = m Prelude.>>= k
  return = Prelude.return

main :: IO ()
main =
  putStrLn "What is your name:" >>= \_ -> 
  getLine >>= \name -> 
  putStrLn name

```
